plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
    maven {
        name "KotlinForForge"
        url "https://thedarkcolour.github.io/KotlinForForge/"
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    // runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")
    // compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    // testCompileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")

    implementation ("me.shedaniel.cloth:cloth-config-neoforge:$cloth_config_version")
    implementation ("dev.architectury:architectury-neoforge:$architectury_version")

    implementation ("me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}")
    
    // runtimeOnly("dev.emi:emi-neoforge:${emi_version}")
    // compileOnly("dev.emi:emi-neoforge:${emi_version}:api")
    // testCompileOnly("dev.emi:emi-neoforge:${emi_version}:api")

    implementation("dev.isxander:yet-another-config-lib:${yacl_version}-neoforge") {
        exclude group: "net.neoforged.fancymodloader", module: "loader"
    }

    // for testing
    runtimeOnly "curse.maven:energized-power-782147:6447946"
}

loom {
    neoForge {
        accessWidenerPath = project(":common").file("src/main/resources/hauntfurnace.accesswidener")
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    atAccessWideners.add("hauntfurnace.accesswidener")
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = BETA
    modLoaders.add("neoforge")

    modrinth {
        projectId = "G05u0Ety"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")

        minecraftVersions.add("1.21.5")

        requires("architectury-api")
        requires("yacl")
        optional("rei")

        announcementTitle = "Neoforge version on Modrinth"
    }

    curseforge {
        projectId = "875936"
        projectSlug = "haunt-furnace"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        
        minecraftVersions.add("1.21.5")
        javaVersions.add(JavaVersion.VERSION_21)

        requires("architectury-api")
        requires("yacl")
        optional("roughly-enough-items")

        announcementTitle = "Neoforge version on Curseforge"
    }
}
