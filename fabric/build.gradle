plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    include modApi("teamreborn:energy:$reborn_energy_version") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    // modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    // modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    modCompileOnly ("me.shedaniel.cloth:cloth-config-fabric:$cloth_config_version")
    modCompileOnly ("dev.architectury:architectury-fabric:$architectury_version")

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"
    
    // modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    // modRuntimeOnly "dev.emi:emi-fabric:${emi_version}"

    modImplementation("dev.isxander:yet-another-config-lib:${yacl_version}-fabric")
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"

    // For testing
    modRuntimeOnly "curse.maven:jade-324717:6389824"
    modRuntimeOnly "curse.maven:energized-power-782147:6447322"
}

loom {
    accessWidenerPath = project(":common").file("src/main/resources/hauntfurnace.accesswidener")
    runs {
        datagen {
            inherit client
            "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/generated")}"
            vmArg "-Dfabric-api.datagen.modid=hauntfurnace"
            runDir "build/datagen"
        }
    }
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

publishMods {
    file = remapJar.archiveFile
    changelog = file("../CHANGELOG.md").getText()
    type = BETA
    modLoaders.add("fabric")

    modrinth {
        projectId = "G05u0Ety"
        accessToken = providers.environmentVariable("MODRINTH_TOKEN")

        minecraftVersions.add("${minecraft_version}")

        requires("architectury-api")
        requires("yacl")
        optional("rei")

        announcementTitle = "Fabric version on Modrinth"
    }

    curseforge {
        projectId = "875936"
        projectSlug = "haunt-furnace"
        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")

        minecraftVersions.add("${minecraft_version}")
        javaVersions.add(JavaVersion.VERSION_21)

        requires("architectury-api")
        requires("yacl")
        optional("roughly-enough-items")

        announcementTitle = "Fabric version on Curseforge"
    }
}
